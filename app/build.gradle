plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-parcelize'
  id 'kotlin-kapt'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion 30

  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }

  defaultConfig {
    applicationId "com.pranisheba.vet"
    minSdkVersion 21
    targetSdkVersion 30
    versionCode 1
    versionName "1.0"
    signingConfig signingConfigs.release
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      buildConfigField("String", "BASE_URL", "\"http://182.16.159.22:8682/FFMSAPI/api/\"")

      applicationVariants.all { variant ->
        def date = new Date()
        def formattedDate = date.format('HHmmss-dd-MM-yyyy')
        variant.outputs.all {
          outputFileName = "psVet_${variant.buildType.name}_v${defaultConfig.versionCode}_${formattedDate}.apk"
        }
      }
    }
    debug {
      buildConfigField("String", "BASE_URL", "\"http://192.168.0.117:8084/FFMS/api/\"")
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = '1.8'
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.21"
  implementation 'androidx.core:core-ktx:1.6.0'
  implementation 'androidx.appcompat:appcompat:1.3.1'
  implementation 'com.google.android.material:material:1.4.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
  implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
  implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
  implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
  implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
  implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
  implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  testImplementation 'junit:junit:4.13.2'
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

  // Retrofit with Moshi Converter
  implementation "com.squareup.retrofit2:retrofit:2.9.0"
  implementation "com.squareup.retrofit2:converter-gson:2.9.0"

  // Logger for Retrofit
  implementation "com.squareup.okhttp3:logging-interceptor:4.7.2"

  // Webkit
  implementation "androidx.webkit:webkit:1.4.0"
}
